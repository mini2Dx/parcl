import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.9.0'
	}
}

repositories {
	mavenCentral()
	maven { url "https://mini2dx.org/maven/content/repositories/thirdparty" }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven'
apply plugin: 'signing'

if(project.hasProperty('ossrhUser') && project.hasProperty("release")) {
	apply plugin: "io.codearte.nexus-staging"

	nexusStaging {
		username = ossrhUser
		password = ossrhPassword
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.mini2Dx'
version = '1.7.0'
description = 'Gradle plugin for bundling your Java application for distribution on Windows, Mac and Linux'

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile files('libs/appbundler-1.1.0.jar')
	compile "com.github.spullara.mustache.java:compiler:0.8.17"

	testCompile 'junit:junit:4.11'
}

task jarWithAppBundler(type: Jar) {
	from { zipTree(configurations.compile.find { it.getName().contains('appbundler') }) }
    from sourceSets.main.output
	classifier = ''
}
jarWithAppBundler.dependsOn jar

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar, jarWithAppBundler
}


if (Os.isFamily(Os.FAMILY_WINDOWS)) {
	task buildExe(type:Exec) {
		workingDir 'natives/windows'
	
		commandLine 'cmd', '/c', 'MSBuild', 'parcl.sln', '/p:Configuration=Release', '/t:rebuild'
	}
	
	task copyExeToJar(type: Copy, dependsOn: buildExe) {
		from 'natives/windows/bin/Release'
		include '*.exe'
		include '*.pdb'
		into 'src/main/resources'
	}
	
	assemble.dependsOn copyExeToJar
}

// Maven Central publication
if(project.hasProperty('ossrhUser')) {
	signing {
		sign configurations.archives
	}
	
	uploadArchives {
		repositories {
			mavenDeployer {
				// POM signature
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
				// Target repository
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUser, password: ossrhPassword)
				}
				pom.project {
					name project.name
					description project.description
					packaging 'jar'
					url 'https://github.com/mini2Dx/parcl'

					scm {
						connection 'scm:git:https://github.com/mini2Dx/parcl.git'
						developerConnection 'scm:git:git@github.com:mini2Dx/parcl.git'
						url 'https://github.com/mini2Dx/parcl.git'
					}

					licenses {
						license {
							name 'The MIT License (MIT)'
							url 'https://opensource.org/licenses/MIT'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id = 'tomcashman'
							name = 'Thomas Cashman'
							email = 'thomascashman404@gmail.com'
						}
					}
				}
			}
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}
